Protocol ASW:

Declarations:
    aenc/2;
    h/1;

Types:
    Agent Alice,Bob;
    Number NA,NB,m;
    Function h;
    Mapping pk,sk;

Knowledge:
    Alice : Alice, Bob, pk(Alice), sk(Alice), pk(Bob), m;
    Bob   : Alice, Bob, pk(Bob), sk(Bob), pk(Alice);

Public:
    pk(Alice), pk(Bob);

Private:
    sk(Alice), sk(Bob);

Actions:
    [s1] Alice -> Bob (NA) : aenc{ pk(Alice) . pk(Bob) . m . h(NA) }sk(Alice);
    [s2] Bob -> Alice (NB) : aenc{ aenc{ pk(Alice) . pk(Bob) . m . h(NA) }sk(Alice) . h(NB) }sk(Bob);
    [s3] Alice -> Bob      : NA;
    [s4] Bob -> Alice      : NB;

Goals:
    Alice ->* Bob : m;
    Bob ->* Alice : m;
    Alice -> Bob : m;
    /* The following goals are NOT achieved in ASW:
       - Bob -> Alice : m
       - m secret between Alice,Bob
    */

ChannelKeys:

end
Protocol ASW:

Declarations:
    aenc/2;
    h/1;

Types:
    Agent A,B;
    Number NA,NB,m;
    Function h;
    Mapping pk,sk

Knowledge:
    A : A, B, pk(A), sk(A), pk(B), m;
    B : A, B, pk(B), sk(B), pk(A);

Public:
    pk(A), pk(B);

Private:
    sk(A), sk(B);

Actions:
    [s1] A -> B (NA) : aenc{ pk(A) . pk(B) . m . h(NA) }sk(A);
    [s2] B -> A (NB) : aenc{ aenc{ pk(A) . pk(B) . m . h(NA) }sk(A) . h(NB) }sk(B);
    [s3] A -> B      : NA;
    [s4] B -> A      : NB;

Goals:
    /* Weak (non-injective) agreement: both agree on m */
    A ->* B : m;
    B ->* A : m;
    /* Injective agreement: A strongly authenticates B on m */
    A -> B : m;
    /* The following goals are NOT achieved in ASW:
       - B -> A : m (injective authentication of A by B)
       - m secret between A,B (m is exposed by signatures)
    */

ChannelKeys:

end
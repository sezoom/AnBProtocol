Protocol NSCK:

Declarations:
   senc/2;
   pre/2;

Types:
    Agent A,B,S;
    Number NA,NB;
    Data tagnsck1,tagnsck2,tagnsck3,tagnsck4,tagnsck5,tagnsck6,tagnsck7;
    Symmetric_key skAS,skBS,KAB;
    Function pre;

Knowledge:
    A : A, B, S, skAS, tagnsck1, tagnsck2, tagnsck3, tagnsck4, tagnsck5, tagnsck6, tagnsck7;
    B : A, B, S, skBS, tagnsck1, tagnsck2, tagnsck3, tagnsck4, tagnsck5, tagnsck6, tagnsck7;
    S : A, B, S, skAS, skBS, tagnsck1, tagnsck2, tagnsck3, tagnsck4, tagnsck5, tagnsck6, tagnsck7;
Public:
    A, B, S, tagnsck1, tagnsck2, tagnsck3, tagnsck4, tagnsck5, tagnsck6, tagnsck7;
Private:
    skAS, skBS;

Actions:
    [m1] A -> S (NA) : tagnsck3 . A . B . NA;
    [m2] S -> A (KAB) : senc{ tagnsck6 . NA . B . KAB . senc{ tagnsck4 . KAB . A }skBS }skAS;
    [m3] A -> B      : senc{ tagnsck4 . KAB . A }skBS;
    [m4] B -> A (NB) : senc{ tagnsck1 . NB }KAB;
    [m5] A -> B      : senc{ tagnsck5 . pre(tagnsck7, NB) }KAB;

Goals:
    B ->* A : NB;
    KAB secret between A,B,S;

ChannelKeys:
    K(A,B): KAB;
    K(B,A): KAB;
end
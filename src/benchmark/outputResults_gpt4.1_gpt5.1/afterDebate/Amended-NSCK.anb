Protocol AmendedNSCK:

Declarations:
    senc/2;
    pre/1;

Types:
    Agent A,B,S;
    Number nA,nB0,nB;
    Symmetric_key sk;
    Symmetric_key kAB;
    Function pre;
    Mapping sk/2

Knowledge:
    A : A, B, S, sk(A,S), sk(B,S);
    B : B, A, S, sk(B,S), sk(A,S);

Public:

Private:
    sk(A,S), sk(B,S), kAB;

Actions:
    [m1] A -> B           : A;
    [m2] B -> A (nB0)     : senc{ A . nB0 }sk(B,S);
    [m3] A -> S (nA)      : A . B . nA . senc{ A . nB0 }sk(B,S);
    [m4] S -> A (kAB)     : senc{ nA . B . kAB . senc{ kAB . nB0 . A }sk(B,S) }sk(A,S);
    [m5] A -> B           : senc{ kAB . nB0 . A }sk(B,S);
    [m6] B -> A (nB)      : senc{ nB }kAB;
    [m7] A -> B           : senc{ pre(nB) }kAB;

Goals:
    B -> A : (A,B,kAB,nB);
    A ->* B : (A,B,kAB,nB);
    A -> S : (A,B,kAB,nA);
    B ->* S : (A,B,kAB,nB0);
    kAB secret between A,B,S;
    nB secret between A,B;
    nB0 secret between B,S;

ChannelKeys:

end
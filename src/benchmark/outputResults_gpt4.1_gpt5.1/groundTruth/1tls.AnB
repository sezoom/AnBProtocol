Protocol TLS:

Declarations:
    aenc/2;
    senc/2;
    hash/1;
    prf/3;
    clientK/3;
    serverK/3;
Types:
    Agent A,B,s;
    Number NA,NB,Sid,PA,PB,PMS,tagTLS1,tagTLS2,tagTLS3,tagTLS4,tagTLS5,tagTLS6,tagTLS7,tagTLS8,tagTLS9;
    Function hash,clientK,serverK,prf;
    Mapping pk
Knowledge:
    A: pk(A), sk(A), pk(B), pk(s);
    B: pk(B), sk(B), pk(A), pk(s);

Public:
    pk(A),pk(B);
Private:
    sk(A), sk(B);

Actions:
    [m1] A -> B (NA, Sid, PA) : A . NA . Sid . PA . tagTLS4;
    [m2] B -> A (NB)         : NB . Sid . PB . aenc{B . pk(B)}sk(s);
    [m3] A -> B (PMS)        : aenc{A . pk(A)}sk(s)
                               . aenc{PMS}pk(B)
                               . aenc{hash(NB . B . PMS . tagTLS5)}sk(A)
                               . senc{hash(prf(PMS, NA, NB) . A . B . NA . NB . Sid . PA . PB . PMS . tagTLS3)}clientK(NA, NB, prf(PMS, NA, NB));
    [m4] B -> A              : senc{hash(prf(PMS, NA, NB) . A . B . NA . NB . Sid . PA . PB . PMS . tagTLS2)}serverK(NA, NB, prf(PMS, NA, NB));

Goals:
    /*  B authenticates A on prf(PMS,NA,NB); */
    B -> A : prf(PMS, NA, NB)
    prf(PMS,NA,NB) secret between A,B;

ChannelKeys:
    K(A,B): clientK(NA,NB,prf(PMS,NA,NB));
    K(B,A): serverK(NA,NB,prf(PMS,NA,NB));

end


Protocol Kerberos:

Declarations:
    senc/2;

Types:
    Agent A,B,C,G,S;
    Number n1,n2,t1,t2,t3,payload;
    Symmetric_key sk, kCG, kCS;
    Mapping sk/2

Knowledge:
    C : C, A, G, S, sk(C,A);
    A : C, A, G, S, sk(C,A), sk(A,G);
    G : C, A, G, S, sk(A,G), sk(G,S);
    S : C, A, G, S, sk(G,S);

Public:

Private:
    sk(C,A), sk(A,G), sk(G,S);

Actions:
    [k1] C -> A (n1) :
        C . G . n1;

    [k2] A -> C (kCG, t1) :
        senc{ kCG . C . t1 }sk(A,G) .
        senc{ kCG . n1 . t1 . G }sk(C,A);

    [k3] C -> G (n2) :
        senc{ kCG . C . t1 }sk(A,G) .
        senc{ C . t1 }kCG .
        S . n2;

    [k4] G -> C (kCS, t2) :
        senc{ kCS . C . t2 }sk(G,S) .
        senc{ kCS . n2 . t2 . S }kCG;

    [k5] C -> S (t3) :
        senc{ kCS . C . t2 }sk(G,S) .
        senc{ C . t3 }kCS;

    [k6] S -> C :
        senc{ t3 }kCS .
        senc{ payload }kCS;

Goals:
    S *->* C : payload;
    kCG secret between C,A,G;
    kCS secret between C,G,S;
    payload secret between C,G,S;

ChannelKeys:

end
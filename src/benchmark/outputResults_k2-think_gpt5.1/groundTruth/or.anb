Protocol OTWAY_REES:

Declarations:
    senc/2;

Types:
    Agent A,B,S;
    Number n1,n2,i;
    Symmetric_key key;
    Mapping k

Knowledge:
    A: k(A,S), B;
    B: k(B,S);
    S: k(A,S), k(B,S);

Public:

Private:

Actions:
    [or1] A -> B (n1, i) :
        i . A . B . senc{ n1 . i . A . B }k(A,S);
    [or2] B -> S (n2) :
        i . A . B . senc{ n1 . i . A . B }k(A,S) .
        senc{ n2 . i . A . B }k(B,S);
    [or3] S -> B (key) :
        i . senc{ n1 . key }k(A,S) .
        senc{ n2 . key }k(B,S);
    [or4] B -> A :
        i . senc{ n1 . key }k(A,S);

Goals:
  /* A injectively agrees with B on key; */
  A -> B : key;
  /* B injectively agrees with A on key; */
  B -> A : key;
  key secret between A,B;

ChannelKeys:

end
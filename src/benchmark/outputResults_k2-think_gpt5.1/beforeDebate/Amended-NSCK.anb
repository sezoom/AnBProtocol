Protocol AmendedNSCK:

Declarations:
    senc/2;
    pre/1;

Types:
    Agent A,B,S;
    Number nA, nB0, nB;
    Symmetric_key sk(A,S), sk(B,S), kAB;
    Function pre;
    Mapping ;

Knowledge:
    A : A, B, S, sk(A,S);
    B : A, B, S, sk(B,S);
    S : A, B, S, sk(A,S), sk(B,S);

Public:

Private:
    sk(A,S), sk(B,S);

Actions:
    [step1] A -> B : A;
    [step2] B -> A (nB0) : senc{ A . nB0 }sk(B,S);
    [step3] A -> S (nA) : A . B . nA . senc{ A . nB0 }sk(B,S);
    [step4] S -> A (kAB) : senc{ nA . B . kAB . senc{ kAB . nB0 . A }sk(B,S) }sk(A,S);
    [step5] A -> B : senc{ kAB . nB0 . A }sk(B,S);
    [step6] B -> A (nB) : senc{ nB }kAB;
    [step7] A -> B : senc{ pre(nB) }kAB;

Goals:
    B -> A : A . B . kAB . nB;
    A ->* B : A . B . kAB . nB;
    A -> S : A . B . kAB . nA;
    B ->* S : A . B . kAB . nB0;
    kAB secret between A,B,S;
    nB secret between A,B;
    nB0 secret between B,S;

ChannelKeys:

end
Protocol NSLKeyServer:

Declarations:
    aenc/2;

Types:
    Agent A,B,S;
    Number NA,NB;
    Data   tagNSLKeyServer1,tagNSLKeyServer2,tagNSLKeyServer3,tagNSLKeyServer4,tagNSLKeyServer5,tagNSLKeyServer6,tagNSLKeyServer7;
    Mapping pk,sk;

Knowledge:
    A : A, B, S, pk(A), sk(A), pk(S);
    B : B, A, S, pk(B), sk(B), pk(S);
    S : S, A, B, pk(S), sk(S), pk(A), pk(B);

Public:
    A, B, S, pk(A), pk(B), pk(S);

Private:
    sk(A), sk(B), sk(S);

Actions:
    /* Phase 1: A gets B's public key from S */
    [m1] A -> S             : tagNSLKeyServer1 . A . B;
    [m2] S -> A             : aenc{ tagNSLKeyServer2 . B . pk(B) }sk(S);

    /* Phase 2: A initiates NSL with B */
    [m3] A -> B (NA)        : aenc{ tagNSLKeyServer3 . NA . A }pk(B);

    /* Phase 3: B gets A's public key from S */
    [m4] B -> S             : tagNSLKeyServer4 . B . A;
    [m5] S -> B             : aenc{ tagNSLKeyServer5 . A . pk(A) }sk(S);

    /* Phase 4: B responds to A, completing the NSL exchange */
    [m6] B -> A (NB)        : aenc{ tagNSLKeyServer6 . NA . NB . B }pk(A);
    [m7] A -> B             : aenc{ tagNSLKeyServer7 . NB }pk(B);

Goals:
    /* B authenticates A on the nonce NA */
    B -> A : NA;

    /* A authenticates B on the nonce NB */
    A -> B : NB;

ChannelKeys:

end
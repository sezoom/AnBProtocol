Protocol NSPK:

Declarations:
    aenc/2;

Types:
    Agent A,B,S;
    Number NA,NB;
    Mapping pk,sk;

Knowledge:
    A : A, B, S, pk(S), pk(A), sk(A);
    B : B, A, S, pk(S), pk(B), sk(B);
    S : S, A, B, pk(A), pk(B), pk(S), sk(S);

Public:
    pk(A), pk(B), pk(S);

Private:
    sk(A), sk(B), sk(S);

Actions:
    [step1] A -> S     : A . B;
    [step2] S -> A     : aenc{ pk(B) . B }pk(A);
    [step3] A -> B (NA): aenc{ NA . A }pk(B);
    [step4] B -> S     : B . A;
    [step5] S -> B     : aenc{ pk(A) . A }pk(B);
    [step6] B -> A (NB): aenc{ NA . NB }pk(A);
    [step7] A -> B     : aenc{ NB }pk(B);

Goals:
    A -> B : NA;
    B -> A : NB;
    NA secret between A,B;
    NB secret between A,B;

ChannelKeys:

end
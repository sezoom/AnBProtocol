Protocol TaggedKeyExchange:

Declarations:
    aenc/2;

Types:
    Agent A,B,S;
    Number NA,NB,tag1,tag2,tag3,tag4,tag5;
    Mapping pk,sk;

Knowledge:
    A : A, B, S, pk(A), sk(A), pk(S);
    B : A, B, S, pk(B), sk(B), pk(S);
    S : A, B, S, pk(S), sk(S), pk(A), pk(B);

Public:
    pk(A), pk(B), pk(S);

Private:
    sk(A), sk(B), sk(S);

Actions:
    [p1] A -> S     : A . B;
    [p2] S -> A     : aenc{ tag1 . B . pk(B) }sk(S);
    [p3] A -> B (NA): aenc{ tag2 . A . NA }pk(B);
    [p4] B -> S     : B . A;
    [p5] S -> B     : aenc{ tag3 . A . pk(A) }sk(S);
    [p6] B -> A (NB): aenc{ tag4 . B . NA . NB }pk(A);
    [p7] A -> B     : aenc{ tag5 . NB }pk(B);

Goals:
    A -> B : NA;
    B -> A : NB;
    NA secret between A,B;
    NB secret between A,B;

ChannelKeys:

end
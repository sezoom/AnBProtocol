Protocol OtwayRees:

Declarations:
    senc/2;
    Mapping sk/1;

Types:
    Agent A,B,S;
    Number C,R1,R2;
    Symmetric_key K1,K2,KC;

Knowledge:
    A : A, B, S, sk(A), K1, K2;
    B : A, B, S, sk(B), K1, K2;
    S : A, B, S, sk(A), sk(B), K1, K2;

Public:
    A, B, S;

Private:
    sk(A), sk(B), K1, K2, KC;

Actions:
    [m1] A -> B (C,R1) : C . A . B . senc{ R1 . C . A . B }K1;
    [m2] B -> S (R2)   : C . A . B . senc{ R1 . C . A . B }K1 . senc{ R2 . C . A . B }K2;
    [m3] S -> B (KC)   : C . senc{ R1 . KC }K1 . senc{ R2 . KC }K2;
    [m4] B -> A        : C . senc{ R1 . KC }K1;

Goals:
    B -> A : KC;
    A -> B : KC;
    KC secret between A,B;

ChannelKeys:
    K(A,B): KC;
    K(B,A): KC;

end